; графическая программа - рисует квадраты 50х50 в режиме 13h
; с плавным переходом атрибутов (исп. "усреднение")
; выход - по нажатию Esc
; автор - Пилюк Н., ФПМ_21_1_гр.

        .186
        .model tiny
	.code
	org 100h

start:  mov bx,cs         ;в bx - seg сегмента кода
        add bh,20h        ;прибавляем 2000h к bx
        mov ds,bx         ;и сохраняем в ds (там будет буфер картинки)
        mov ax,13h        ;в ax - номер видеорежима
        int 10h           ;включаем видеорежим 13h

Main:   push ds           ;seg буфера - в стек
        pop es            ;и затем в es
        in ax,40h         ;берём "случайное" число (из таймера)
        shl ax,4          ;умножаем его на 16
        mov di,ax         ;смещение квадрата ("случайное")
        mov al,255        ;цвет квадрата
        mov bx,50         ;высота = 50

pl:     add di,270        ;di+270 (320-ширина(50))
        mov cx,50         ;50 байтов
        rep stosb         ;копируем в буфер
        dec bx            ;уменьшим bx
        jnz pl            ;и пока bx не станет нулём

        mov bh,0FAh       ;в bx 0FA00h (т.к bl=0)

Sglazh: mov al,[bx+1]     ; al <- цвет справа
        mov cl,[bx-1]     ; cl <- цвет слева
        add ax,cx         ;прибавим его к ax
        mov cl,[bx-320]   ; cl <- цвет сверху
        add ax,cx         ;прибавим его снова к ax
        mov cl,[bx+320]   ; cl <- цвет снизу
        add ax,cx         ;и снова прибавим его к ax
        shr ax,2          ;поделим на 4
        mov [bx],al       ;и запишем "сглаженный" цвет в буфер
        dec bx            ;уменьшим bx
        jnz Sglazh        ;и продолжаем пока он не 0

                          ;копируем рисунок в видеопамять
        mov ax,0A000h     ;seg VGA
        mov es,ax         ;запишем в es
        mov ch,0FAh       ;столько байтов скопировать в VGA
        xor di,di         ;обнулим смещение VGA
        xor si,si         ;обнулим смещение буфера
        rep movsb         ;выполняем копирование
        in al,60h         ;клавиша была нажата?
        dec al            ;это был Esc?
        jnz Main          ;нет, продолжаем - следующий квадрат
        ret               ;да, закончим работу

	end start
